import 'intersection' as intersection
import 'vector3D' as vector
import 'lambertian' as lambertian

class Plane {
    init(objHash) {
        self.center = vector.Vector3D(
            objHash['center']['x'],
            objHash['center']['y'],
            objHash['center']['z'])
        self.normal = vector.Vector3D(
            objHash['normal']['x'],
            objHash['normal']['y'],
            objHash['normal']['z']).normalise()
        self.width = objHash['width']
        self.length = objHash['length']
        self.colour = vector.Vector3D(
            objHash['colour']['r'],
            objHash['colour']['g'],
            objHash['colour']['b'])
        self.shininess = objHash['shininess']
        if (objHash.has_key?('material')) {
            var material = objHash['material']
            if (material == 'glass') {
                self.material = glass.Glass()
            }
            else if (material == 'lambertian') {
                self.material = lambertian.Lambertian()
            }
            else {
                throw Exception('Material not found!')
            }
        }
        else {
            self.material = lambertian.Lambertian()
        }
    }

    intersect(ray) {
        var denominator = self.normal.dot(ray.direction)
        if (denominator.absolute_value() > vector.EPSILON) {
            var t = (self.center - ray.position).dot(self.normal) / denominator
            if (t >= vector.EPSILON) {
                return intersection.Intersection(t, ray.position * t, self.normal, self)
            }
        }
        return nil
    }

    scatter(incident, collision) {
        return self.material.scatter(incident, collision)
    }
}
